#!/usr/bin/perl -w
# objdump-info -- lintian collection script

# The original shell script version of this script is
# Copyright © 1998 Christian Schwarz
#
# This version, including support for etch's binutils, is
# Copyright © 2008 Adam D. Barratt
# Copyright © 2019 Felix Lechner
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, you can find it on the World Wide
# Web at http://www.gnu.org/copyleft/gpl.html, or write to the Free
# Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston,
# MA 02110-1301, USA.

package Lintian::coll::objdump_info;

no lib '.';

use strict;
use warnings;
use autodie;

use FileHandle;
use IO::Async::Loop;
use Path::Tiny;

use lib "$ENV{'LINTIAN_ROOT'}/lib/";

use Lintian::Collect;
use Lintian::Util qw(locate_helper_tool gzip safe_qx_ioasync);

use constant EMPTY => q{};

my $helper = locate_helper_tool('coll/objdump-info-helper');

sub collect {
    my ($pkg, $type, $dir) = @_;
    my $info = Lintian::Collect->new($pkg, $type, $dir);

    chdir("$dir/unpacked");

    my $uncompressed;
    foreach my $path ($info->sorted_index) {
        next if not $path->is_file;

        my $name = $path->name;
        my $info = $info->file_info($name);

        # must be elf or static library
        next
          unless $info =~ m/\bELF\b/
          || ($info =~ m/\bcurrent ar archive\b/ && $name =~ m/\.a$/);

        my $output = safe_qx_ioasync($helper, $name);
        $uncompressed .= $output;
    }

    # write even if empty; binaries check depends on it
    gzip($uncompressed // EMPTY, "$dir/objdump-info.gz");

    return;
}

collect(@ARGV) unless caller;

1;

# Local Variables:
# indent-tabs-mode: nil
# cperl-indent-level: 4
# End:
# vim: syntax=perl sw=4 sts=4 sr et
